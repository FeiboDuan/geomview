AC_INIT

AC_CANONICAL_SYSTEM
# The following vars contain info about the local system:
#   (AC_CANONICAL_SYSTEM sets them)
#       var		value on RedHat 6.1
#       ---		-------------------
#       build           i686-pc-linux-gnu
#       host            i686-pc-linux-gnu
#       target          i686-pc-linux-gnu
#       build_alias     i686-pc-linux-gnu
#       host_alias      i686-pc-linux-gnu
#       target_alias    i686-pc-linux-gnu
#       build_cpu       i686
#       build_vendor    pc
#       build_os        linux-gnu
#       host_cpu        i686
#       host_vendor     pc
#       host_os         linux-gnu
#       target_cpu      i686
#       target_vendor   pc
#       target_os       linux-gnu

AM_INIT_AUTOMAKE(geomview,1.8.0)
AM_CONFIG_HEADER(config.h)

AC_MSG_RESULT([configuring $PACKAGE $VERSION])

AC_PREFIX_DEFAULT(/usr/local/Geomview)

AC_PROG_CC
GEOM_AC_PROG_CXX
if test "$CXX" = "" ; then
  HAVE_CXX=false
  case "$target" in
    *linux*)
  AC_MSG_ERROR([
    Cannot find a working C++ compiler.  Geomview requires C++
    on Linux systems.
  ])
    ;;
    *)
  AC_MSG_WARN([

    Configuring without a C++ compiler (couldn't find one on
    your system).  C++ is only used only to compile the vrml->OOGL
    converter, so your distribution will be compiled without it.
  ])
    ;;
  esac
else
  HAVE_CXX=true
fi

AM_PROG_LEX
AC_PROG_YACC
AC_PROG_RANLIB
AC_PROG_INSTALL

# Check for ways to generate a PDF file.
# GNU Texinfo has a texi2pdf, otherwise use a DVI->PDF converter.
AC_CHECK_PROGS(TEXI2PDF, texi2pdf)
AC_CHECK_PROGS(DVIPDF, dvipdf)

AM_CONDITIONAL(HAVE_TEXI2PDF, test x$TEXI2PDF != x)

########################################################################
########################################################################
########################################################################

# If --with-motif=DIR was specified, set MOTIF_DIR to DIR
AC_ARG_WITH(motif,
	    [--with-motif=DIR      set DIR to motif installation prefix],
	    MOTIF_DIR=$withval
           )

AC_ARG_WITH(opengl,
	    [--with-opengl=DIR      set DIR to opengl installation prefix],
	    OPENGL_DIR=$withval
           )

########################################################################

#
# AC_PATH_X sets $x_libraries and $x_includes
#
AC_PATH_X

#
# If $x_includes is nonempty, remember it in XINCLUDES
#
if test "$x_includes" != "" ; then
  XINCLUDES=-I$x_includes
else
  XINCLUDES=
fi
AC_SUBST(XINCLUDES)

#
# Find out how to link with the various X libraries we need
#
GEOM_FIND_L_OPTION(-lX11,
		   XOpenDisplay,
		   ["" "$x_libraries" "$MOTIF_DIR/lib"])
X11_L_OPTION=$GEOM_L_OPTION
GEOM_FIND_L_OPTION(-lXt,
		   XtCreateManagedWidget,
		   ["" "$x_libraries" "$MOTIF_DIR/lib"])
if test "$GEOM_L_OPTION" != "$X11_L_OPTION" ; then
  Xt_L_OPTION=$GEOM_L_OPTION
else
  Xt_L_OPTION=""
fi
GEOM_FIND_L_OPTION(-lXext,
		   XextCreateExtension,
		   ["" "$x_libraries" "$MOTIF_DIR/lib"])
if test "$GEOM_L_OPTION" != "$X11_L_OPTION"  \
     -a "$GEOM_L_OPTION" != "$Xt_L_OPTION"  ; then
  Xext_L_OPTION=$GEOM_L_OPTION
else
  Xext_L_OPTION=""
fi

# Store the whole shebang in $XLIBS:
XLIBS="$X11_L_OPTION -lX11 $Xt_L_OPTION -lXt $Xext_L_OPTION -lXext"
AC_SUBST(XLIBS)

AC_CACHE_VAL(ac_cv_XLIBS,[ac_cv_XLIBS="XLIBS='$XLIBS'"])

########################################################################

GEOM_FIND_L_OPTION(-lXmu,
		   XmuLookupStandardColormap,
                   ["" "$x_libraries" "$MOTIF_DIR/lib"],
		   $XLIBS)
if test "$GEOM_L_OPTION" != "0" ; then
  HAVE_XMU=1
  if test "$GEOM_L_OPTION" != "" ; then
    XMULIBS="$GEOM_L_OPTION -lXmu"
  else 
    XMULIBS="-lXmu"
  fi
else
  HAVE_XMU=0
fi

if test "$HAVE_XMU" = "1" ; then
  AC_DEFINE(HAVE_XMU)
fi
AC_SUBST(XMULIBS)

########################################################################

if test "$MOTIF_DIR" != "" ; then
  USER_MOTIF_DIR="$MOTIF_DIR/include"
else
  USER_MOTIF_DIR=""
fi
GEOM_FIND_I_OPTION(Xm/Xm.h, ["$USER_MOTIF_DIR" "" "$x_includes" "/usr/local/include"])
if test "$GEOM_I_OPTION" = "0" ; then
    AC_MSG_ERROR([

Can't find Motif header file Xm/Xm.h.  Geomview requires Motif
(or Lesstif).  See the file INSTALL.Geomview for details.

])
fi

MOTIFINCLUDE=$GEOM_I_OPTION
AC_SUBST(MOTIFINCLUDE)

########################################################################

if test "$MOTIFLIBS" = "" ; then
  MOTIFLIBS="-lXm"
fi

if test "$MOTIF_DIR" != "" ; then
  USER_MOTIF_DIR="$MOTIF_DIR/lib"
else
  USER_MOTIF_DIR=""
fi
GEOM_FIND_L_OPTION($MOTIFLIBS,
		   XmCreateForm,
		   ["$USER_MOTIF_DIR" "" "$x_libraries"],
		   $XLIBS
		  )
if test "$GEOM_L_OPTION" = "0" ; then
    AC_MSG_ERROR([

Can't find Motif libraries.  Geomview requires Motif
(or Lesstif).  See the file INSTALL.Geomview for details.

])
fi
MOTIF_L_OPTION=$GEOM_L_OPTION
MOTIFLIBS="$GEOM_L_OPTION $MOTIFLIBS"
AC_SUBST(MOTIFLIBS)

########################################################################

if test "$OPENGL_DIR" != "" ; then
  USER_OPENGL_DIR="$OPENGL_DIR/include"
else
  USER_OPENGL_DIR=""
fi
GEOM_FIND_I_OPTION(GL/gl.h, ["$USER_OPENGL_DIR" "" "/usr/local/include"])
if test "$GEOM_I_OPTION" = "0" ; then
    AC_MSG_ERROR([

Can't find OpenGL header file GL/gl.h.  Geomview requires OpenGL
(or Mesa).  See the file INSTALL.Geomview for details.

])
fi
OPENGLINCLUDE=$GEOM_I_OPTION
AC_SUBST(OPENGLINCLUDE)

########################################################################

if test "$OPENGL_DIR" != "" ; then
  USER_OPENGL_DIR="$OPENGL_DIR/lib"
else
  USER_OPENGL_DIR=""
fi
GEOM_FIND_L_OPTION(-lGL,
		   glBegin,
		   ["$USER_OPENGL_DIR" "" "$x_libraries"],
		   "$XLIBS -lm")
if test "$GEOM_L_OPTION" = "0" ; then
    AC_MSG_ERROR([

Can't find OpenGL libraries.  Geomview requires OpenGL (or Mesa).
See the file INSTALL.Geomview for details.

])
fi
GL_L_OPTION=$GEOM_L_OPTION
GEOM_FIND_L_OPTION(-lGLU,
		   gluBeginPolygon,
		   ["$USER_OPENGL_DIR" "" "$x_libraries"],
		   "$GL_L_OPTION -lGL $XLIBS -lm")
if test "$GEOM_L_OPTION" != "$GL_L_OPTION" ; then
  GLU_L_OPTION=$GEOM_L_OPTION
else
  GLU_L_OPTION=""
fi
OPENGLLIBS="$GL_L_OPTION -lGL $GLU_L_OPTION -lGLU"
AC_SUBST(OPENGLLIBS)

########################################################################

AC_ARG_WITH(forms,
	    [--with-forms=DIR      set DIR to xforms installation dir],
	    [ if test "$withval" = "no" ; then
                TRY_XFORMS=false
              else
                TRY_XFORMS=true
                XFORMS_DIR=$withval
              fi ;
            ],
            [ TRY_XFORMS=true ]
           )

AC_ARG_WITH(xforms,
	    [--with-xforms=DIR      same as --with-forms],
	    [ if test "$withval" = "no" ; then
                TRY_XFORMS=false
              else
                TRY_XFORMS=true
                XFORMS_DIR=$withval
              fi ;
            ],
            [ TRY_XFORMS=true ]
           )

########################################################################
########################################################################
########################################################################
AC_MSG_CHECKING([for xforms])

AC_CACHE_VAL(ac_cv_have_xforms,
[if $TRY_XFORMS ; then
    if test "$XFORMS_DIR" != "" ; then
      USER_XFORMS_DIR="$XFORMS_DIR"
    else
	# If the 'xforms' directory is present, look for a
	# subdirectory of it called XFORMS (perhaps more than one level
	# down).  If found, use that as the XFORMS_DIR directory.  If
	# there's more than one XFORMS subdir, use the first one found.
	# (There should never be more than one, but in case there is,
	# we need a definite behavior to resolve the ambiguity.)
	if test -d xforms ; then
	  USER_XFORMS_SUBDIR=`find xforms -name FORMS -print | sed -e '2,$d'`
	  USER_XFORMS_DIR="`pwd`/$USER_XFORMS_SUBDIR"
	fi
    fi
    GEOM_FIND_I_OPTION(forms.h, ["$USER_XFORMS_DIR" "" "/usr/local/include"])
    ac_xforms_include=$GEOM_I_OPTION
    GEOM_FIND_L_OPTION(-lforms,
    		   fl_initialize,
    		   ["$USER_XFORMS_DIR" "" "/usr/local/lib"],
    		   "$XLIBS -lm"
    		  )
    if test "$GEOM_L_OPTION" != "0" ; then
      XFORMS_L_OPTION=$GEOM_L_OPTION
      ac_xforms_libs="$XFORMS_L_OPTION -lforms"
      ac_have_xforms=yes
    else
      ac_xforms_libs=""
      ac_have_xforms=no
    fi
else
  ac_xforms_libs=""
  ac_have_xforms=no
fi
if test "$ac_have_xforms" = "no" ; then
  ac_cv_have_xforms="have_xforms=no"
else
  ac_cv_have_xforms="have_xforms=yes \
	ac_xforms_include='$ac_xforms_include' ac_xforms_libs='$ac_xforms_libs'"
fi
])
eval $ac_cv_have_xforms



########################################################################
########################################################################
########################################################################

if test "$have_xforms" = "no" ; then
  AC_MSG_RESULT([no])
  AC_MSG_WARN([

    Configuring without the XFORMS library.  Several of Geomview's
    external modules use the XFORMS library, and they will not be
    compiled unless you re-run configure with the argument
    --with-xforms=DIR where DIR is the pathname of the directory
    containing both the XFORMS header file xforms.h, and the XFORMS
    library files (libforms.a, libforms.so, etc).  Geomview itself
    does not use XFORMS, so this is fine if all you want is
    Geomview.

])
else
  AC_MSG_RESULT([headers $ac_xforms_include, libraries $ac_xforms_libs])
fi

XFORMSINCLUDE=$ac_xforms_include
XFORMSLIBS=$ac_xforms_libs

AC_SUBST(XFORMSLIBS)
AC_SUBST(XFORMSINCLUDE)
AC_SUBST(XFORMS_LIB_SUBDIRS)

########################################################################

OPENGL_LD_LIBRARY_PATH=`expr "$GL_L_OPTION" : '-L\(.*\)'`
MOTIF_LD_LIBRARY_PATH=`expr "$MOTIF_L_OPTION" : '-L\(.*\)'`
XFORMS_LD_LIBRARY_PATH=`expr "$XFORMS_L_OPTION" : '-L\(.*\)'`

if test "$OPENGL_LD_LIBRARY_PATH" != "0" ; then
  LOCAL_LD_LIBRARY_PATH=$OPENGL_LD_LIBRARY_PATH
fi
if test "$MOTIF_LD_LIBRARY_PATH" != "0" ; then
  if test "$LOCAL_LD_LIBRARY_PATH" != "" ; then
    LOCAL_LD_LIBRARY_PATH="$LOCAL_LD_LIBRARY_PATH:$MOTIF_LD_LIBRARY_PATH"
  else
    LOCAL_LD_LIBRARY_PATH="$MOTIF_LD_LIBRARY_PATH"
  fi
fi
if test "$XFORMS_LD_LIBRARY_PATH" != "0" ; then
  if test "$LOCAL_LD_LIBRARY_PATH" != "" ; then
    LOCAL_LD_LIBRARY_PATH="$LOCAL_LD_LIBRARY_PATH:$XFORMS_LD_LIBRARY_PATH"
  else
    LOCAL_LD_LIBRARY_PATH="$XFORMS_LD_LIBRARY_PATH"
  fi
fi
if test "$LOCAL_LD_LIBRARY_PATH" != "" ; then
  LD_LIBRARY_PATH_SETTINGS="LD_LIBRARY_PATH=$LOCAL_LD_LIBRARY_PATH"
  AC_SUBST(LD_LIBRARY_PATH_SETTINGS)
fi

########################################################################
########################################################################
########################################################################

AC_ARG_ENABLE(fstropen-1,
	      [--enable-fstropen-1           use manual fstropen rather than fmemopen],
	      [ if test "$enableval" != "no" ; then
                  AC_DEFINE(USE_FSTROPEN_1, 1)
                fi ]
              )

AC_ARG_ENABLE(d1debug,
	      [--enable-d1debug           turn on D1 level debugging output],
	      [ if test "$enableval" != "no" ; then
                  AC_DEFINE(D1_DEBUG, 1)
                fi ]
              )

AC_ARG_ENABLE(motion-averaging,
[--enable-motion-averaging      turn on experimental motion averaging (default is off)],
	      [ if test "$enableval" = "no" ; then
                  AC_DEFINE(EXPERIMENTAL_MOTION_AVERAGING, 0)
                else
                  AC_DEFINE(EXPERIMENTAL_MOTION_AVERAGING, 1)
                fi ],
              [ AC_DEFINE(EXPERIMENTAL_MOTION_AVERAGING, 0) ]
              )

########################################################################
########################################################################
########################################################################

case "$target" in
 *linux*) MACHTYPE="linux"
	  AC_DEFINE_UNQUOTED(MACHTYPE, "$MACHTYPE")
          AC_DEFINE(__linux__, 1)
          # Note: we only need c++ on linux, for src/lib/oogl/util/glibglue.C
          #   AC_PROG_CXX
	  # Not any more.  C++ is really needed for the vrml2oogl/lib stuff
	  # on all platforms.   [mbp Thu Oct 12 20:30:42 2000]
          AC_LANG_CPLUSPLUS
          AC_TRY_LINK([ #include <streambuf.h>
                        #include <strstream.h>
                        #include <stdiostream.h>
                        #include <iostream.h> ],
 
                      [ char *mem = 0;
                        int len = 0;
                        new strstreambuf(mem, len); ],
 
                      STRSTREAMLIB=-lstdc++,
                      STRSTREAMLIB=-liostream
                     )
          AC_LANG_C
          AC_SEARCH_LIBS(__setfpucw, c,
                         AC_DEFINE(HAVE_SETFPUCW, 1),
                         AC_DEFINE(HAVE_SETFPUCW, 0))
          LINUX=true
          case "$target" in
            *powerpc*) AC_DEFINE(ALISTADDR, [])
                       ;;
          esac
          ;;
  *irix*) MACHTYPE="sgi"
	  AC_DEFINE_UNQUOTED(MACHTYPE, "$MACHTYPE")
	  AC_DEFINE(sgi, 1)
          if test "$CC" = "cc" ; then
            CFLAGS="$CFLAGS -float -XSVR4 -common"
            LIBS="$LIBS -lmalloc"
          fi
          ;;
*rs6000*) MACHTYPE="rs6000"
	  AC_DEFINE_UNQUOTED(MACHTYPE, "$MACHTYPE")
          AC_DEFINE(AIX, 1)
	  AC_DEFINE(ACCEPT_ARG3_TYPE, [unsigned long])
          AC_DEFINE(unix, 1)
          AC_DEFINE(alloca, __alloca)
	  ;;
       *) MACHTYPE=$target_os
	  AC_DEFINE_UNQUOTED(MACHTYPE, "$MACHTYPE")
          ;;
esac

AC_DEFINE(BINARY_POPEN_REQUIRES_B, 0)

AC_SUBST(MACHTYPE)

AM_CONDITIONAL(HAVE_CXX, test x$HAVE_CXX = xtrue)
AM_CONDITIONAL(LINUX,         test x$LINUX = xtrue)
AM_CONDITIONAL(HAVE_XFORMS,    test x$have_xforms = xyes)

# What this actually checks is whether a program that includes
# <math.h> and calls 'sin' needs -lm to link properly.  I put this
# test here because on my linux system, -lm seems unnecessary
# (<math.h> seems to define everything with inline code).  Perhaps it
# would be harmless to just give -lm in all cases, but what the heck;
# might as well check for it since we can.  It's possible that this
# could cause problems if on some system <math.h> deals with sin() but
# not with some other function that we call, but we'll deal with that
# if/when it comes up (by adding that function to this test, for
# example).
# mbp Mon May  8 18:47:33 2000
AC_MSG_CHECKING([whether we need to link -lm to get math functions])
AC_TRY_LINK([ #include <math.h> ],
            [ double s = sin(1.5); ],
	    [ MATHLIB=""
	      AC_MSG_RESULT([no]) ],
	    [ MATHLIB=-lm
	      AC_MSG_RESULT([yes]) ]
	   )
AC_SUBST(MATHLIB)
# MATHLIB is now "-lm" on systems that need it, or empty on those that don't.
# So it's safe to put $(MATHLIB) in link commands.


AC_MSG_CHECKING([whether we need to link -lsocket to get socket()])
AC_TRY_LINK([ #include <sys/socket.h> ],
            [ int s = socket(AF_UNIX, SOCK_STREAM, 0); ],
            [ SOCKETLIBS=""
              AC_MSG_RESULT([no]) ],
            [ SOCKETLIBS=-lsocket
              AC_MSG_RESULT([yes]) ]
           )
AC_SUBST(SOCKETLIBS)


AC_SEARCH_LIBS(fmemopen, c,
               AC_DEFINE(HAVE_FMEMOPEN, 1),
               AC_DEFINE(HAVE_FMEMOPEN, 0))

# AC_MSG_CHECKING([whether we have fmemopen()])
# AC_TRY_LINK([ #include <stdio.h>
# static char buffer[] = "foobar"; ],
#             [ (void)fmemopen(buffer, strlen(buffer), "r"); ],
#             [ AC_DEFINE(HAVE_FMEMOPEN, 1)
#               AC_MSG_RESULT([yes]) ],
#             [ AC_DEFINE(HAVE_FMEMOPEN, 0)
#               AC_MSG_RESULT([no]) ]
#            )


########################################################################

AC_TYPE_SIZE_T

moduledir="\$(exec_prefix)/bin"
AC_SUBST(moduledir)

geomdatadir="\$(prefix)/data"
AC_SUBST(geomdatadir)

# cache 'moduledir' and 'geomdatadir' so individual module configure scripts
# can get them from the cache
AC_CACHE_VAL(ac_cv_moduledir,[ac_cv_moduledir="moduledir='$moduledir'"])
AC_CACHE_VAL(ac_cv_geomdatadir,[ac_cv_geomdatadir="geomdatadir='$geomdatadir'"])

########################################################################

top_geom_builddir='$(top_builddir)'
AC_SUBST(top_geom_builddir)

OOGLLIBS_ONCE="\$(top_geom_builddir)/src/lib/oogl/wa/libwa.a                \
          \$(top_geom_builddir)/src/lib/camera/libcamera.a                  \
          \$(top_geom_builddir)/src/lib/color/libcolor.a                    \
          \$(top_geom_builddir)/src/lib/fexpr/libfexpr.a                    \
          \$(top_geom_builddir)/src/lib/geomutil/bdy/libbdy.a               \
          \$(top_geom_builddir)/src/lib/geomutil/crayplutil/libcrayplutil.a \
          \$(top_geom_builddir)/src/lib/geomutil/dim/libdim.a               \
          \$(top_geom_builddir)/src/lib/geomutil/plutil/libplutil.a         \
          \$(top_geom_builddir)/src/lib/gprim/bbox/libbbox.a                \
          \$(top_geom_builddir)/src/lib/gprim/bezier/libbezier.a            \
          \$(top_geom_builddir)/src/lib/gprim/comment/libcomment.a          \
          \$(top_geom_builddir)/src/lib/gprim/discgrp/libdg.a               \
          \$(top_geom_builddir)/src/lib/gprim/geom/libgeom.a                \
          \$(top_geom_builddir)/src/lib/gprim/inst/libinst.a                \
          \$(top_geom_builddir)/src/lib/gprim/lincoln/liblincoln.a          \
          \$(top_geom_builddir)/src/lib/gprim/list/liblist.a                \
          \$(top_geom_builddir)/src/lib/gprim/mesh/libmesh.a                \
          \$(top_geom_builddir)/src/lib/gprim/ndmesh/libndmesh.a            \
          \$(top_geom_builddir)/src/lib/gprim/npolylist/libnpolylist.a      \
          \$(top_geom_builddir)/src/lib/gprim/polylist/libpolylist.a        \
          \$(top_geom_builddir)/src/lib/gprim/quad/libquad.a                \
          \$(top_geom_builddir)/src/lib/gprim/skel/libskel.a                \
          \$(top_geom_builddir)/src/lib/gprim/sphere/libsphere.a            \
          \$(top_geom_builddir)/src/lib/gprim/tlist/libtlist.a              \
          \$(top_geom_builddir)/src/lib/gprim/vect/libvect.a                \
          \$(top_geom_builddir)/src/lib/mg/buf/liblibmgbuf.a                \
          \$(top_geom_builddir)/src/lib/mg/common/libmgcommon.a             \
          \$(top_geom_builddir)/src/lib/mg/opengl/libmgopengl.a             \
          \$(top_geom_builddir)/src/lib/mg/ps/libmgps.a                     \
          \$(top_geom_builddir)/src/lib/mg/rib/libmgrib.a                   \
          \$(top_geom_builddir)/src/lib/mib/libmib.a                        \
          \$(top_geom_builddir)/src/lib/pointlist/libpointlist.a            \
          \$(top_geom_builddir)/src/lib/shade/libshade.a                    \
          \$(top_geom_builddir)/src/lib/window/libwindow.a                  \
          \$(top_geom_builddir)/src/lib/geometry/cmodel/libcmodel.a         \
          \$(top_geom_builddir)/src/lib/geometry/hpoint3/libhpoint3.a       \
          \$(top_geom_builddir)/src/lib/geometry/hpointn/libhpointn.a       \
          \$(top_geom_builddir)/src/lib/geometry/point3/libpoint3.a         \
          \$(top_geom_builddir)/src/lib/geometry/transform3/libtransform3.a \
          \$(top_geom_builddir)/src/lib/geometry/transformn/libtransformn.a \
          \$(top_geom_builddir)/src/lib/geometry/transobj/libtransobj.a     \
          \$(top_geom_builddir)/src/lib/oogl/lisp/liblisp.a                 \
          \$(top_geom_builddir)/src/lib/oogl/refcomm/librefcomm.a           \
          \$(top_geom_builddir)/src/lib/oogl/util/libooglutil.a"

AC_SUBST(OOGLLIBS_ONCE)
AC_CACHE_VAL(ac_cv_OOGLLIBS_ONCE,[ac_cv_OOGLLIBS_ONCE="OOGLLIBS_ONCE='$OOGLLIBS_ONCE'"])

# STRSTREAMLIB was (potentially) set above
AC_SUBST(STRSTREAMLIB)
AC_CACHE_VAL(ac_cv_STRSTREAMLIB,[ac_cv_STRSTREAMLIB="STRSTREAMLIB='$STRSTREAMLIB'"])

AC_CACHE_VAL(ac_cv_OOGLLIBS,[ac_cv_OOGLLIBS="OOGLLIBS='\$(OOGLLIBS_ONCE) \$(OOGLLIBS_ONCE) \$(OOGLLIBS_ONCE) \$(STRSTREAMLIB)'"])
eval $ac_cv_OOGLLIBS
AC_SUBST(OOGLLIBS)

STUBLIB="\$(top_builddir)/src/lib/gprim/stub/libstub.a"
AC_SUBST(STUBLIB)

########################################################################

### These are here to make sure that the timestamps on these C files,
### which are included with the distribution, are more recent than
### the timestamps on the corresponding yacc and lex source files, so
### that 'make' won't try to run yacc and/or lex to regenerate them.
### Normal users shouldn't need lex/yacc --- they just need a C compiler
### to compile these files.
###
### Note: These 'touch' commands SHOULDN'T be necessary, since
### running 'make dist' after 'make' leaves the timestamps on the
### .c files newer than the source .y and .l files.  However, we
### leave them here anyway because of some problems reported by
### dpvc@union.edu when configuring on his LinuxPPC system; they
### seem to be necessary on that system.

touch src/lib/oogl/wa/wa.yacc.c
touch src/lib/oogl/wa/wa.lex.c

########################################################################

LF_LINK_HEADERS(src/lib/oogl/lisp		\
		src/lib/oogl/refcomm		\
		src/lib/oogl/util		\
		src/lib/oogl/wa			\
		src/lib/geometry/cmodel		\
		src/lib/geometry/hpoint3	\
		src/lib/geometry/hpointn	\
		src/lib/geometry/point3		\
		src/lib/geometry/transform3	\
		src/lib/geometry/transformn	\
		src/lib/geometry/transobj	\
		src/lib/camera			\
		src/lib/color			\
		src/lib/fexpr			\
		src/lib/geomutil/bdy		\
		src/lib/geomutil/crayplutil	\
		src/lib/geomutil/dim		\
		src/lib/geomutil/plutil		\
		src/lib/gprim/bbox		\
		src/lib/gprim/bezier		\
		src/lib/gprim/comment		\
		src/lib/gprim/discgrp		\
		src/lib/gprim/geom		\
		src/lib/gprim/inst		\
		src/lib/gprim/lincoln		\
		src/lib/gprim/list		\
		src/lib/gprim/mesh		\
		src/lib/gprim/polylist		\
		src/lib/gprim/ndmesh		\
		src/lib/gprim/npolylist		\
		src/lib/gprim/quad		\
		src/lib/gprim/skel		\
		src/lib/gprim/sphere		\
		src/lib/gprim/stub		\
		src/lib/gprim/tlist		\
		src/lib/gprim/vect		\
		src/lib/mg/buf			\
		src/lib/mg/common		\
		src/lib/mg/opengl		\
		src/lib/mg/ps			\
		src/lib/mg/rib			\
		src/lib/mib			\
		src/lib/pointlist		\
		src/lib/shade			\
		src/lib/window			\
		src/lib/forms			\
	 	src/bin/geomview/common		\
		)

LF_SET_INCLUDES


module_srcdirs=`find src/bin -name 'configure.in' -print | sed -e 's/\/configure.in$//g'`
module_srcdirs=`echo $module_srcdirs`
src_bin_module_subdirs=`echo $module_srcdirs | sed -e 's/src\/bin\///g'`
AC_SUBST(src_bin_module_subdirs)

AC_CONFIG_SUBDIRS($module_srcdirs)


AC_OUTPUT(Makefile
	  m4/Makefile
	  data/Makefile
	  doc/Makefile
	  doc/html/Makefile
	  src/Makefile
	  src/lib/Makefile
	  src/lib/camera/Makefile
	  src/lib/color/Makefile
	  src/lib/fexpr/Makefile
	  src/lib/oogl/Makefile
	  src/lib/oogl/lisp/Makefile
	  src/lib/oogl/refcomm/Makefile
	  src/lib/oogl/util/Makefile
	  src/lib/oogl/wa/Makefile
	  src/lib/geometry/Makefile
	  src/lib/geometry/cmodel/Makefile
	  src/lib/geometry/hpoint3/Makefile
	  src/lib/geometry/hpointn/Makefile
	  src/lib/geometry/point3/Makefile
	  src/lib/geometry/transform3/Makefile
	  src/lib/geometry/transformn/Makefile
	  src/lib/geometry/transobj/Makefile
	  src/lib/geomutil/Makefile
	  src/lib/geomutil/bdy/Makefile
	  src/lib/geomutil/crayplutil/Makefile
	  src/lib/geomutil/dim/Makefile
	  src/lib/geomutil/plutil/Makefile
	  src/lib/gprim/Makefile
	  src/lib/gprim/bbox/Makefile
	  src/lib/gprim/bezier/Makefile
	  src/lib/gprim/comment/Makefile
	  src/lib/gprim/discgrp/Makefile
	  src/lib/gprim/geom/Makefile
	  src/lib/gprim/inst/Makefile
	  src/lib/gprim/lincoln/Makefile
	  src/lib/gprim/list/Makefile
	  src/lib/gprim/mesh/Makefile
	  src/lib/gprim/polylist/Makefile
	  src/lib/gprim/ndmesh/Makefile
	  src/lib/gprim/npolylist/Makefile
	  src/lib/gprim/quad/Makefile
	  src/lib/gprim/skel/Makefile
	  src/lib/gprim/sphere/Makefile
	  src/lib/gprim/stub/Makefile
	  src/lib/gprim/tlist/Makefile
	  src/lib/gprim/vect/Makefile
	  src/lib/mg/Makefile
	  src/lib/mg/buf/Makefile
	  src/lib/mg/common/Makefile
	  src/lib/mg/opengl/Makefile
	  src/lib/mg/ps/Makefile
	  src/lib/mg/rib/Makefile
	  src/lib/mib/Makefile
	  src/lib/pointlist/Makefile
	  src/lib/shade/Makefile
	  src/lib/window/Makefile
	  src/lib/forms/Makefile
	  src/bin/Makefile
	  src/bin/geomview/Makefile
	  src/bin/geomview/common/Makefile
	  src/bin/geomview/x11/Makefile
	  src/bin/geomview/x11/bitmaps/Makefile
	  src/bin/geomview/x11/interface/Makefile
	  src/bin/example/Makefile
	  src/bin/ginsu/Makefile
	  src/bin/tackdown/Makefile
	  src/bin/animate/Makefile
	  src/bin/animate/interface/Makefile
	  src/bin/graffiti/Makefile
	  src/bin/transformer/Makefile
	  src/bin/stereo/Makefile
	  src/bin/geomutil/Makefile
	  src/bin/geomutil/bdy/Makefile
	  src/bin/geomutil/clip/Makefile
	  src/bin/geomutil/maple2oogl/Makefile
	  src/bin/geomutil/math2oogl/Makefile
	  src/bin/geomutil/oogl2rib/Makefile
	  src/bin/geomutil/oogl2vrml/Makefile
	  src/bin/geomutil/plutil/Makefile
	  src/bin/geomutil/ucd/Makefile
	  src/bin/geomutil/vrml2oogl/Makefile
	  src/bin/geomutil/vrml2oogl/lib/Makefile
	  src/bin/sweep/Makefile
)

#	  src/bin/cplxview/Makefile
#	  src/bin/maniview/Makefile
#	  src/bin/flythrough/Makefile
#	  src/bin/hinge/Makefile
